# BSBM BI Q4
#
# Note: Sesame xsd:string prevents running the original query.  The modified
# version works fine.
#
# Last Changed Rev: 5682
# Last Changed Date: 2011-11-17 20:09:12 -0500 (Thu, 17 Nov 2011)
# solutions=100, chunks=1, elapsed=56500ms.
# solutions=100, chunks=1, elapsed=55114ms.
#
# The sub-selects are very similar, but ?feature is pulled out in the 2nd one.

prefix bsbm: <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/vocabulary/>
prefix bsbm-inst: <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/instances/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

Select ?feature ((?sumF*(?countTotal-?countF))/(?countF*(?sumTotal-?sumF)) As ?priceRatio)
  {
#    { Select (count(?price) As ?countTotal) (sum(xsd:float(xsd:string(?price))) As ?sumTotal)
     { Select (count(?price) As ?countTotal) (sum(?price) As ?sumTotal)
      {
        ?product a <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/instances/ProductType3> .
        ?offer bsbm:product ?product ;
               bsbm:price ?price .
      }
    }
#    { Select ?feature (count(?price2) As ?countF) (sum(xsd:float(xsd:string(?price2))) As ?sumF)
    { Select ?feature (count(?price2) As ?countF) (sum(?price2) As ?sumF)
      {
        ?product2 a <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/instances/ProductType3> ;
                 bsbm:productFeature ?feature .
        ?offer2 bsbm:product ?product2 ;
               bsbm:price ?price2 .
      }
      Group By ?feature
    }
  }
 Order By desc(?priceRatio) ?feature
 Limit 100
